plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.3.0' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.6' apply false
    id 'me.champeau.jmh' version '0.7.3' apply false
    id 'jacoco'
}

def npmCommand = org.gradle.internal.os.OperatingSystem.current().isWindows() ? 'npm.cmd' : 'npm'
def npxCommand = org.gradle.internal.os.OperatingSystem.current().isWindows() ? 'npx.cmd' : 'npx'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    apply plugin: 'jacoco'

    group = 'io.github.ehlyzov.branchline'
    version = 'v0.11.0-SNAPSHOT'

    detekt {
        config = files("$rootDir/detekt.yml")
        buildUponDefaultConfig = true
        ignoreFailures = true
        autoCorrect = false
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.6"
    }

    // Note: KMP modules have target-specific test tasks (e.g., jvmTest).
    // This config applies only where the conventional 'test' task exists.
    tasks.matching { it.name == 'test' }.configureEach { task ->
        if (task instanceof Test) {
            task.useJUnitPlatform {
                excludeTags 'performance'
            }
        }
    }

    // Configure Kotlin toolchains/options depending on plugin used
    plugins.withId('org.jetbrains.kotlin.jvm') {
        kotlin {
            jvmToolchain(21)
            compilerOptions {
                freeCompilerArgs.add("-Xnested-type-aliases")
                freeCompilerArgs.add("-Xexpect-actual-classes")
            }
        }
    }
    plugins.withId('org.jetbrains.kotlin.multiplatform') {
        kotlin {
            jvmToolchain(21)
            compilerOptions {
                freeCompilerArgs.add("-Xexpect-actual-classes")
            }
        }
    }

    jacoco {
        toolVersion = '0.8.13'
    }

    tasks.matching { it.name == 'jacocoTestReport' }.configureEach { report ->
        // Depend on 'test' only when it exists (non-KMP modules)
        if (tasks.findByName('test') != null) report.dependsOn 'test'
        report.reports { r ->
            r.xml.required.set(true)
            r.html.required.set(true)
        }
    }

    afterEvaluate {
        tasks.withType(Test).forEach { testTask ->
            def reportTask = tasks.register("${testTask.name}JacocoReport", JacocoReport) {
                dependsOn testTask
                executionData(testTask)
                classDirectories.setFrom(
                    files(
                        fileTree(dir: "$buildDir/classes/kotlin/jvm/main"),
                        fileTree(dir: "$buildDir/classes/kotlin/main"),
                        fileTree(dir: "$buildDir/classes/java/main"),
                    )
                )
                sourceDirectories.setFrom(files("src/main/kotlin", "src/jvmMain/kotlin", "src/commonMain/kotlin"))
                reports { r ->
                    r.xml.required.set(true)
                    r.html.required.set(true)
                }
                onlyIf { classDirectories.files.any { it.exists() } }
            }

            def verifyTask = tasks.register("${testTask.name}JacocoCoverageVerification", JacocoCoverageVerification) {
                dependsOn testTask
                executionData(testTask)
                classDirectories.setFrom(
                    files(
                        fileTree(dir: "$buildDir/classes/kotlin/jvm/main"),
                        fileTree(dir: "$buildDir/classes/kotlin/main"),
                        fileTree(dir: "$buildDir/classes/java/main"),
                    )
                )
                sourceDirectories.setFrom(files("src/main/kotlin", "src/jvmMain/kotlin", "src/commonMain/kotlin"))
                violationRules {
                    rule {
                        limit {
                            minimum = 0.2
                        }
                    }
                }
                onlyIf { classDirectories.files.any { it.exists() } }
            }

            tasks.named("check").configure {
                dependsOn verifyTask
            }
        }
    }
}

// Delegate run to the VM module's JVM runner
tasks.register('run') {
    group = 'application'
    description = 'Runs io.github.ehlyzov.branchline.vm.VMLoopKt via :vm'
    dependsOn(':vm:runVmLoop')
}

tasks.register('playgroundBuildAssets', Exec) {
    group = 'documentation'
    description = 'Builds the playground static bundle into docs/assets using npm'
    dependsOn ':interpreter:jsBrowserProductionLibraryDistribution'
    workingDir "$projectDir/playground"
    commandLine npmCommand, 'run', 'build'
    inputs.dir("$projectDir/playground/src")
    outputs.dir("$projectDir/docs/assets")
}

tasks.register('docsGenerate', Exec) {
    group = 'documentation'
    description = 'Generates Diplodoc output into docs-compiled'
    dependsOn 'playgroundBuildAssets'
    workingDir projectDir
    commandLine npxCommand, 'yfm', 'build', '-i', 'docs', '-o', 'docs-compiled'
    inputs.dir("$projectDir/docs")
    inputs.dir("$projectDir/docs/assets")
    outputs.dir("$projectDir/docs-compiled")
}

tasks.register('docsBuild') {
    group = 'documentation'
    description = 'Builds playground assets and compiles the documentation site'
    dependsOn 'docsGenerate'
}
