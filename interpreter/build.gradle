plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

kotlin {
    jvm {
        compilations.all { comp ->
            comp.kotlinOptions {
                freeCompilerArgs += ["-Xnested-type-aliases", "-Xexpect-actual-classes"]
            }
        }
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }
    js(IR) {
        browser {
            testTask {
                if (!System.getenv().containsKey("CHROME_BIN")) {
                    enabled = false
                }
            }
        }
        nodejs()
        binaries.library()
        binaries.executable()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.10.0-RC'
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0-RC'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
                implementation 'net.codinux.log:klf:1.8.3'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
            }
        }
        jvmMain {
            dependencies {
                implementation "com.dslplatform:dsl-json:2.0.2"
                implementation "org.jetbrains.kotlin:kotlin-reflect"
                implementation 'org.mapdb:mapdb:3.0.9'
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2"
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
                implementation 'net.jqwik:jqwik:1.8.5'
                implementation project(':test-fixtures')
                implementation project(':vm')
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
            }
        }
    }
}

def jsProductionLibraryDir = layout.buildDirectory.dir("dist/js/productionLibrary")

tasks.named('jsNodeProductionLibraryDistribution').configure {
    dependsOn 'jsProductionExecutableCompileSync'
}

tasks.named('jsBrowserProductionLibraryDistribution').configure {
    dependsOn 'jsProductionExecutableCompileSync'
}

tasks.named('jsBrowserProductionWebpack').configure {
    dependsOn 'jsProductionLibraryCompileSync'
}

tasks.register('packageInterpreterJs', org.gradle.api.tasks.bundling.Tar) {
    group = 'distribution'
    description = 'Package the JS production library for Node distribution.'
    dependsOn 'jsNodeProductionLibraryDistribution'
    archiveBaseName.set('branchline-interpreter')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('js')
    archiveExtension.set('tgz')
    compression = org.gradle.api.tasks.bundling.Compression.GZIP
    from(jsProductionLibraryDir)
}
